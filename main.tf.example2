terraform {
  required_providers {
    bigip = {
      source  = "F5Networks/bigip"
      version = "1.4.0"
    }
  }
  required_version = ">= 0.13"
}
provider "bigip" {
  address  = var.address
  username = var.username
  password = var.password
}
# download rpm
resource "null_resource" "download_do" {
  provisioner "local-exec" {
    command = "wget ${var.do_rpm_url}"
  }
}
# install DO rpm to BIG-IP
resource "null_resource" "install_do" {
  provisioner "local-exec" {
    command = "./install_do.sh ${var.address} admin:${var.password} ${var.do_rpm}"
  }
  depends_on = [null_resource.download_do]
}
resource "bigip_do" "do-example" {
  do_json    = file("do.json")
  depends_on = [null_resource.install_do]
}
# download as3 rpm
resource "null_resource" "download_as3" {
  provisioner "local-exec" {
    command = "wget ${var.as3_rpm_url}"
  }
  depends_on = [bigip_do.do-example]
}
# install AS3 rpm to BIG-IP
resource "null_resource" "install_as3" {
  provisioner "local-exec" {
    command = "./install_as3.sh ${var.address} admin:${var.password} ${var.as3_rpm}"
  }
  depends_on = [null_resource.download_as3]
}
resource "bigip_as3" "http_as3" {
  as3_json   = file("example1.json")
  depends_on = [null_resource.install_as3]
}
resource "bigip_as3" "http_waf_as3" {
  as3_json   = file("example2.json")
  depends_on = [null_resource.install_as3]
}